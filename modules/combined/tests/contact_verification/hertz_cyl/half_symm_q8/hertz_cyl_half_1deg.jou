title 'Hertz Cylinder Contact (Half Symmetry)'

# Setup dimensions of problem
#{W_f=5.0}     Width of foundation
#{H_f=1.0}      Height of foundation
#{R=3.0}        Radius of cylinder
#{theta=1}     Contact angle (degrees)
#{rfac=0.75}    Radial factor for splitting geometry in radial dir
#{nelem_cit=10}  Number of elements in cylinder inner section theta dir
#{bias_cit=0.8}   Biasing for cylinder inner section theta dir
#{nelem_cot=20}  Number of elements in cylinder outer section theta dir
#{bias_cot=0.8}   Biasing for cylinder outer section theta dir
#{nelem_cor=10}   Number of elements in cylinder outer section radial dir
#{bias_cor=0.8}   Biasing for cylinder outer section radial dir

# Foundation
create vertex {-W_f} 0
create vertex {W_f} 0
create vertex {W_f} {H_f}
create vertex {-W_f} {H_f}

# Cylinder
create vertex 0 {H_f + R}
create vertex {R} {H_f + R}
create vertex {R * sind(theta)} {H_f + R * (1.0 - cosd(theta))}
create vertex 0 {H_f + R * (1.0 - rfac * cosd(theta))}
create vertex {rfac * R * cosd(theta)} {H_f + R}
create vertex {rfac * R * sind(theta)} {H_f + R * (1.0- rfac * cosd(theta))}
create vertex 0 {H_f}

create surface vertex 1 2 3 4

create curve arc center vertex 5 7 6
create curve vertex 10 7
create curve vertex 9 6
create curve vertex 9 10

create curve arc center vertex 5 11 7
create curve vertex 11 8
create curve vertex 8 10

merge all

label curve on

create surface curve 5 7 8 6
create surface curve 9 12 11 10
create surface vertex 8 10 9 5

merge all
 
block 1 surf 1
block 2 surf 2
block 3 surf 3
block 4 surf 4

body 2 3 4 copy reflect x

merge all

block 1 elem quad8
block 2 elem quad8
block 3 elem quad8
block 4 elem quad8

block 5 surf 5
block 6 surf 6
block 7 surf 7
block 5 elem quad8
block 6 elem quad8
block 7 elem quad8

# Specify meshing
curve 1 2 3 4 interval 1
curve 10 6 7 22 24 interval {nelem_cor}
curve 9 11 28 27 interval {nelem_cit}
curve 5 8 21 23 25 interval {nelem_cot}

# Specify mesh biasing
curve 6 7 22 24 scheme bias {bias_cor}
curve 10 scheme bias {1.0/bias_cor}
curve 9 11 28 27 scheme bias {bias_cit}
curve 8 21 23 scheme bias {bias_cot}
curve 5 25 scheme bias {1.0/bias_cot}

mesh surf all

sideset 1 curve 1
sideset 2 curve 3
sideset 3 curve 5 9 28 25
sideset 4 curve 7 20 24 29

export mesh 'hertz_cyl_half_1deg.e' overwrite
